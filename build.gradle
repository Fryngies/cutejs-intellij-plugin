buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io'}
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.JetBrains:gradle-grammar-kit-plugin:2018.1.2"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.3.1'
}

group 'org.cutejs'
version '0.1'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.grammarkit'

sourceSets {
    all {
        java.srcDirs += ['src/main/gen']
        kotlin.srcDirs += ['src/main/kotlin']
        resources.srcDirs = ['src/main/resources']
    }
}

sourceCompatibility = 1.8

grammarKit {
    jflexRelease = '1.7.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation fileTree(dir: '/Applications/WebStorm.app/Contents/plugins/JavaScriptLanguage/lib', include: ['JavaScriptLanguage.jar', 'javascript-openapi.jar'])
}

intellij {
    version '2018.1.1'
    alternativeIdePath = '/Applications/WebStorm.app'
    plugins = ['PsiViewer:2018.1']
}

patchPluginXml {
    changeNotes """
      Initial release.<br>
      Features:<br>
      * Layered syntax highlighting
      """
}

import org.jetbrains.grammarkit.tasks.*

def GENERATE_GROUP = 'Generate'

task generateLexer(type: GenerateLexer) {
    source = "src/main/grammar/CuteJS.flex"
    targetDir = "src/main/gen/org/cutejs/lang/lexer"
    targetClass = "_CuteLexer"
    skeleton = "src/main/grammar/idea-flex.skeleton"
    purgeOldFiles = true

    description = 'Generate Lexer Java sources for CuteJS'
    group = GENERATE_GROUP
}

task generateParser(type: GenerateParser) {
    source = "src/main/grammar/CuteJS.bnf"
    targetRoot = 'src/main/gen'
    pathToParser = 'src/main/gen/org/cutejs/lang/CuteParser.java'
    pathToPsiRoot = 'src/main/gen/org/cutejs/lang/psi'
    purgeOldFiles = true

    description = 'Generate Parser Java sources for CuteJS'
    group = GENERATE_GROUP

    // patch up to date check
    outputs.upToDateWhen { false }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileKotlin.dependsOn(generateLexer, generateParser)