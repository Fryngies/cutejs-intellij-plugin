buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io'}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.JetBrains:gradle-grammar-kit-plugin:2018.1.2"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.3.1'
}

group 'org.cutejs'
version '0.1'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.*

task generateCuteLexer(type: GenerateLexer) {
    source = "src/main/grammar/CuteJS.flex"
    targetDir = "src/main/gen/org/cutejs/lang/lexer"
    targetClass = "_CuteLexer"
    skeleton = "src/main/grammar/idea-flex.skeleton"
    purgeOldFiles = true
}

task generateCuteParser(type: GenerateParser) {
    source = "src/main/grammar/CuteJS.bnf"
    targetRoot = 'src/main/gen'
    pathToParser = 'src/main/gen/org/cutejs/lang/CuteParser.java'
    pathToPsiRoot = 'src/main/gen/org/cutejs/lang/psi'
    purgeOldFiles = true
}

sourceCompatibility = 1.8

grammarKit {
    // version of IntelliJ patched JFlex (see bintray link below), Default is 1.7.0
    jflexRelease = '1.7.0'

    // tag or short commit hash of Grammar-Kit to use (see link below). Default is 2017.1.2
    grammarKitRelease = '34fd65b92a'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    sourceSets {
        main {
            java.srcDirs += ['src/main/gen']
            kotlin.srcDirs += ['src/main/kotlin']
            resources.srcDirs = ['src/main/resources']
        }
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version '2018.1.1'
    sourceSets {
        main {
            java.srcDirs += ['src/main/gen']
            kotlin.srcDirs += ['src/main/kotlin']
            resources.srcDirs = ['src/main/resources']
        }
    }
}

patchPluginXml {
    changeNotes """
      Initial release.<br>
      Features:<br>
      * Layered syntax highlighting
      """
}